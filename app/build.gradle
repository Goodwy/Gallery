apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.goodwy.gallery"
        minSdk 23
        targetSdk 33
        versionCode 201
        versionName "2.0.1"
        setProperty("archivesBaseName", "gallery-$versionCode")
        vectorDrawables.useSupportLibrary = true
        buildConfigField("String", "GOOGLE_PLAY_LICENSING_KEY", "\"${getProperty(getProperties('local.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")
        buildConfigField("String", "PRODUCT_ID_X1", "\"${getProperty(getProperties('local.properties'), 'PRODUCT_ID_X1')}\"")
        buildConfigField("String", "PRODUCT_ID_X2", "\"${getProperty(getProperties('local.properties'), 'PRODUCT_ID_X2')}\"")
        buildConfigField("String", "PRODUCT_ID_X3", "\"${getProperty(getProperties('local.properties'), 'PRODUCT_ID_X3')}\"")
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            // we cannot change the original package name, else PhotoEditorSDK won't work
            //applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        if (is_proprietary) {
            main.java.srcDirs += 'src/proprietary/kotlin'
        }
    }

    flavorDimensions "licensing"
    productFlavors {
        proprietary {}
        foss {}
        prepaid {}
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = rootProject.file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

dependencies {
    implementation files('libs/commons-debug.aar')
    //implementation 'com.github.SimpleMobileTools:Simple-Commons:20c2b96dab'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'it.sephiroth.android.exif:library:1.0.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.24'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.9.6'
    implementation 'com.google.vr:sdk-panowidget:1.180.0'
    implementation 'com.google.vr:sdk-videowidget:1.180.0'
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'
    implementation 'info.androidhive:imagefilters:1.0.7'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.github.goodwy:gestureviews:234ff1a0be'
    implementation 'com.github.goodwy:subsampling-scale-image-view:4f4d00a840'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.penfeizhou.android.animation:awebp:2.17.0'
    implementation 'com.github.penfeizhou.android.animation:apng:2.17.0'
    implementation 'com.squareup.okio:okio:3.0.0'
    implementation('com.squareup.picasso:picasso:2.71828') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    compileOnly 'com.squareup.okhttp3:okhttp:4.9.0'

    kapt 'com.github.bumptech.glide:compiler:4.13.2'

    kapt 'androidx.room:room-compiler:2.4.3'
    implementation 'androidx.room:room-runtime:2.4.3'
    annotationProcessor 'androidx.room:room-compiler:2.4.3'

    //implementation project(':commons')

    implementation "com.andrognito.patternlockview:patternlockview:1.0.0"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha04"
    api "com.github.goodwy:RecyclerView-FastScroller:cd067e83e4"
    //api "com.github.tibbi:RecyclerView-FastScroller:5a95285b1f"
    api "com.github.tibbi:reprint:2cb206415d"
    api 'com.google.android.material:material:1.8.0-alpha01'
    api 'com.google.code.gson:gson:2.9.1'
    api 'com.duolingo.open:rtl-viewpager:2.0.0'
    implementation "androidx.core:core-ktx:1.8.0"

    api "com.github.bumptech.glide:glide:4.13.2"
    kapt "com.github.bumptech.glide:compiler:4.13.2"
    annotationProcessor "com.github.bumptech.glide:compiler:4.13.2"

    //My
    implementation 'com.anjlab.android.iab.v3:library:2.0.3'
    implementation 'com.github.Liverm0r:BehavioRule:1.0.1'
    implementation 'com.mikhaellopez:rxanimation:2.1.0'
}

// Apply the PESDKPlugin
if (is_proprietary) {
    apply plugin: 'ly.img.android.sdk'

    imglyConfig {
        vesdk {
            enabled true
            licensePath 'vesdk_android_license'
        }

        pesdk {
            enabled true
            licensePath 'pesdk_android_license'
        }

        modules {
            include 'ui:video-trim'
            include 'ui:core'
            include 'ui:text'
            include 'ui:focus'
            include 'ui:brush'
            include 'ui:filter'
            include 'ui:sticker'
            include 'ui:overlay'
            include 'ui:transform'
            include 'ui:adjustment'

            include 'backend:serializer'
            include 'backend:sticker-smart'
            include 'backend:sticker-animated'

            include 'assets:font-basic'
            include 'assets:filter-basic'
            include 'assets:overlay-basic'
            include 'assets:sticker-shapes'
            include 'assets:sticker-emoticons'
            include 'assets:sticker-animated'
        }
    }
}
